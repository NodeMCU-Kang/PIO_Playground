#include <WiFiManager.h> // https://github.com/tzapu/WiFiManager
// include MDNS
#ifdef ESP8266                  // 若晶片類型是ESP8266
#include <ESP8266mDNS.h>
#include <ESP8266WebServer.h>   // ESP8266網站伺服器程式庫
ESP8266WebServer server(80);    // 建立網站伺服器物件
#elif defined(ESP32)            // 若晶片類型是ESP32
#include <ESPmDNS.h>
#include <WebServer.h>          // ESP32網站伺服器程式庫
WebServer server(80);           // 建立網站伺服器物件
#endif

#define AP_SSID "JarvisAP"      // 自訂的ESP裝置AP名稱
#define AP_PWD  "12345678"      // 自訂的AP密碼
#define TRIGGER_PIN 0           // 啟用「Wi-Fi設置入口」的按鍵接腳

WiFiManager wm;                 // 建立WiFi管理員物件

unsigned int  timeout   = 120; // Wi-Fi管理員的運作秒數
unsigned int  startTime = millis();
bool portalRunning      = false;
bool startAP = true; // 僅啟動網站伺服器，設成true則啟動AP和網站伺服器。

void setup() {
  WiFi.mode(WIFI_STA); // Wi-Fi設置成STA模式；預設模式為STA+AP

  Serial.begin(115200);
  Serial.setDebugOutput(true);
  delay(1000);
  pinMode(TRIGGER_PIN, INPUT_PULLUP);

  wm.setHostname("jarvis");     // 設置ESP的主機名稱
  // wm.setDebugOutput(false);  // 關閉除錯訊息
  wm.autoConnect(AP_SSID, AP_PWD);
  // 確認晶片是否存有Wi-Fi連鍵資料
  if (WiFi.status() == WL_CONNECTED && wm.getWiFiIsSaved()) {
    Serial.println("\n晶片存有Wi-Fi連線資料！");
  } else {
    Serial.println("\n晶片沒有Wi-Fi連線資料…");
  }
  // 設定我們自訂的網站伺服器
  server.on("/", []() {      // 處理”/”路徑的路由
    server.send(200, "text/html; charset=utf-8", "人生最大的風險，<br>就是不願意冒險。");
  });
  server.onNotFound([]() { // 處理「找不到指定資源」的路由
    server.send(404, "text/plain", "File NOT found!");
  });

  server.begin(); // 啟動網站伺服器
}

void loop() {
#ifdef ESP8266
  MDNS.update();
#endif
  doWiFiManager();       // 確認是否啟動Wi-Fi設置介面
  server.handleClient(); // 處理用戶端連線
}

void doWiFiManager() {
  if (portalRunning) {
    wm.process();
    if ((millis() - startTime) > (timeout * 1000)) {
      Serial.println("「Wi-Fi設置入口」操作逾時…");
      portalRunning = false;

      if (startAP) {
        wm.stopConfigPortal();
      } else {
        wm.stopWebPortal();
      }

      server.begin(); // 再次啟動我們的網站伺服器
    }
  }

  // 若啟用「Wi-Fi設置入口」的接腳被按一下…
  if (digitalRead(TRIGGER_PIN) == LOW && (!portalRunning)) {
    server.stop();  // 停止我們自訂的網站伺服器程式

    if (startAP) {
      Serial.println("按鈕被按下了，啟動設置入口。");
      wm.setConfigPortalBlocking(false);
      wm.startConfigPortal(AP_SSID, AP_PWD);
    } else {
      Serial.println("按鈕被按下了，啟動Web入口。");
      wm.startWebPortal();
    }
    portalRunning = true;
    startTime = millis();
  }
}